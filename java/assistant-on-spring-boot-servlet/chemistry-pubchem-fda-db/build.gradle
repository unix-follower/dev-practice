plugins {
    id("java")
    id("io.spring.dependency-management")
    id "jacoco"
    id "pmd"
    id "checkstyle"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-tx"
    implementation "org.hibernate.orm:hibernate-core"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

pmd {
    ignoreFailures = false
    ruleSets = [] // Disable default rulesets
    ruleSetFiles = files("../config/pmd-ruleset.xml")
}

checkstyle {
    configFile = file("../config/checkstyle.xml")
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = false
        csv.required = false
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }

        rule {
            enabled = false
            element = "CLASS"

            limit {
                counter = "LINE"
                value = "TOTALCOUNT"
                maximum = 0.8
            }
        }
    }
}

tasks.named("test") {
    useJUnitPlatform()
}

test {
    finalizedBy jacocoTestReport
}

tasks.withType(Pmd) {
    reports {
        xml.required = true
        html.required = true
        maxFailures = 1
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
        maxWarnings = 1
        maxErrors = 1
    }
}
